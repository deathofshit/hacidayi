import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.0 rc3"

    defaultConfig {
        applicationId "com.android.launcher3"
        minSdkVersion 21
        targetSdkVersion 22
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    productFlavors {
        catapult {
            applicationId "com.kaneoriley.cyanogenport.launcher3"
            versionCode getLongDate().toInteger()
            versionName '12.1-' + getShortDate()
        }
    }

    dependencies {

        compile group: 'com.google.guava', name: 'guava', version: '18.0'
        compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-2'
        compile project(path: ':wallpaperPicker')

    }

    sourceSets {

        main {
            manifest.srcFile '../AndroidManifest.xml'
            java.srcDirs = ['../src', '../util', '../extra/src']
            res.srcDirs = ['../res', '../extra/res']
            proto {
                srcDir '../protos'
            }
        }

    }
}

def getDate(String dateFormat) {
    def df = new SimpleDateFormat(dateFormat)
    Calendar c = Calendar.getInstance();
    TimeZone tz = c.getTimeZone();
    df.setTimeZone(tz)
    return df.format(new Date())
}

def getLongDate() {
    return getDate("yyMMddHH")
}

def getShortDate() {
    return getDate("yyMMdd")
}